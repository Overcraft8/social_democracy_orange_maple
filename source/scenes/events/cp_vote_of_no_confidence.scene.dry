title: The CP calls a vote of no confidence against the current government!
subtitle: We have angered the CP too much...
new-page: true
view-if: ccf_in_government = 1 and ((in_popular_front = 1 and cp_coalition_dissent >= 3) or (in_left_front = 1 and cp_coalition_dissent >= 4)) and ccf_r < 50 and not constructive_vonc
tags: event
go-to: menu

= Vote of No Confidence

The CP has called a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

@menu

- @try_assuage: Try to assuage them somehow.
- @support_center: Could we form a new government based on the Center Party and [+ lpc_name +] (and possibly CPC)?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage the CP?

- @resources: Use our resources to reduce dissent within the coalition.
- @thalmann: Appoint Thälmann as prime_minister instead of [+ prime_minister +].
- @support_center: Could we form a new government?
- @let_it_happen: Never mind. Let the vote happen.

@thalmann
choose-if: monarch == "Woodsworth"
subtitle: This would be pretext for a right-wing coup.
unavailable-subtitle: [? if monarch == "George V" : Monarch George V would never allow this. ?]
on-arrival: prime_minister = "Thälmann"; prime_minister_party = "CP"

With great reluctance, Monarch [+ monarch +] appoints [+ prime_minister +] as prime_minister.

Immediately, [+ prime_minister +] goes to work building a "Soviet Canada": consolidating as much power as possible in the CP, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

In response to [+ prime_minister +] being appointed Prime Minister, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the CP from taking power. We are forced to be on the same side as the CP, whether we want to or not.

- @civil_war


@resources
choose-if: resources >= 3 and cp_ultimatum_seen = 0
unavailable-subtitle: -3 resources - [? if resources < 3 : We do not have enough resources. ?][? if cp_ultimatum_seen : This is beyond the point when resources could fix. ?]
subtitle: -3 resources
on-arrival: resources -= 3; cp_coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.


@support_center
choose-if: (weimar_coalition >= 50 and rpc_relation >= 40) or (grand_coalition >= 50 and rpc_relation >= 45 and cpc_relation >= 30)
unavailable-subtitle: [? if grand_coalition < 50 : This would not give us a majority. ?] [? if rpc_relation < 45 : After working with the Communists, the Center Party does not wish to form a coalition with us. ?] [? if cpc_relation < 30 : The CPC would rather see the government fall than rescue us from the Communists. ?]

We could try to boot the CP from our governing coalition, and form a new government[? if ccf_r <= 50 : with the bourgeois parties ?].

- @election_1928.ccf_majority: Form an CCF government, without the CP?
- @election_1928.weimar_coalition: Form a Weimar Coalition?
- @election_1928.grand_coalition: Form a Grand Coalition?
- @let_it_happen: There is nothing we can do.


@let_it_happen
view-if: not constructive_vonc
on-arrival: cp_relation -= 8; workers_cp -= 5; workers_ccf -= 8; unemployed_ccf -= 6
go-to: let_it_happen_woodsworth if monarch = "Woodsworth"; let_it_happen_georgev if monarch = "George V"

# if Woodsworth is monarch, we are now a caretaker government. If George V is monarch, Brüning is a caretaker government

@let_it_happen_woodsworth
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; ccf_in_government = 0; in_popular_front = 0; in_left_front = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; reichswehr_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I";

The vote succeeds, and our coalition has collapsed. Prime Minister [+ prime_minister +] will lead a caretaker government until the next election.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_georgev
on-arrival: next_election_time = time + 3 if next_election_time >= time + 3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; ccf_in_government = 0; in_popular_front = 0; in_left_front = 0; prime_minister = "Brüning"; prime_minister_party = "RPC "; foreign_minister_party = "CPC"; interior_minister_party = "RPC"; justice_minister_party = "I"; labor_minister_party = "RPC"; reichswehr_minister_party = "I"; economic_minister_party = lpc_name; finance_minister_party = "CPC"; agriculture_minister_party = "RPC"; coalition_dissent = 0; cp_coalition_dissent = 0; pro_republic -= 5; old_middle_ccf -= 3; new_middle_ccf -= 2; workers_ccf -= 3 if pro_republic < 50; workers_ccf -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50

The vote succeeds, and our government has resigned. Prime Minister Brüning has been appointed as the head of an acting government, consisting solely of the bourgeois parties.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

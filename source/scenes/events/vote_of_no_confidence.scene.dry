title: The Right calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
on-arrival: tried_supporting_cp = 0
view-if: ccf_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and coalition_dissent >= 3) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and ccf_r < 50 and not constructive_vonc
tags: event
go-to: vonc_menu

# Options: spend relations to reduce coalition_dissent, ask for cp to abstain
# (lmao CP toleration arc...)

= Vote of No Confidence

[? if in_grand_coalition : The CPC ?][? if in_weimar_coalition or in_popular_front or in_minority_government: The RPC Party ?] has called for a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

@vonc_menu

- @try_assuage: Try to assuage our partners.
- @support_cp: Could we get the CP to abstain?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage our partners?

- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @support_cp: Could we get the CP to abstain?
- @let_it_happen: Never mind. Let the vote happen.

@give_up_prussia
view-if: ccf_prussia == 1
on-arrival: coalition_dissent = 0; ccf_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_ccf -= 5; rural_ccf -= 5; 

Fine. We no longer control the government of Prussia. The vote has been called off.

@austerity
view-if: welfare >= 0 and not secularized
on-arrival: coalition_dissent = 0; workers_ccf -= 10; unemployed_ccf -= 10; workers_cp += 7; workers_nsdap += 3; unemployed_cp += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; cp_coalition_dissent += 1; cp_relation -= 10 if in_popular_front

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
view-if: secularized = 0
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@support_cp
choose-if: (in_grand_coalition and communist_coalition >= 3 and cp_relation >= 50 and cp_r >= cpc_r) or (in_weimar_coalition and cp_relation >= 50 and cp_r >= rpc_r and communist_coalition >= 3)
view-if: tried_supporting_cp = 0
on-arrival: tried_supporting_cp = 1
unavailable-subtitle: [? if communist_coalition < 3 : The CP won't take part in parliamentary government. ?] [? if cp_relation < 50 : The CP is not friendly enough with us. ?] [? if (in_grand_coalition and cp_r < cpc_r) or (in_weimar_coalition and cp_r < rpc_r): The CP does not have enough votes. ?] [? if in_popular_front : We are already in a Popular Front with the CP. ?]

If the Communists abstain, then we essentially can maintain a minority government[? if monarch=="George V":, although it would be very difficult to make policy without the support of Monarch George V, which we do not have ?]. If this happens, then we will own a massive debt to the CP. This could lead to broader cooperation in the future, for better or for worse.

- @support_cp_2: Persuade the CP to "tolerate" us?
- @let_it_happen: Never mind. Let the vote happen.

@support_cp_2
on-arrival: communist_coalition += 1; workers_cp += 3; cp_relation += 5
go-to: support_cp_weimar if in_weimar_coalition; support_cp_grand if in_grand_coalition

# TODO: this can form an infinite loop

The CP is not interested in a "toleration" arrangement; they will only support us if they can participate in the government as part of a coalition. And, if this toleration agreement happens, the right-wing parties will not be inclined to support us. 

@support_cp_grand

- @election_1928.popular_front_coalition: Form a "Popular Front" coalition?
- @election_1928.left_coalition
- @try_assuage: Never mind. We must deal with this some other way.

@support_cp_weimar

- @election_1928.left_coalition
- @try_assuage: Never mind. We must deal with this some other way.


@let_it_happen
call: set_next_election_time
on-arrival: time_to_election = 3; ccf_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; ccf_toleration = 0; foreign_minister_party = "CPC"; interior_minister_party = "RPC"; justice_minister_party = "I"; labor_minister_party = "RPC"; reichswehr_minister_party = "I"; economic_minister_party = lpc_name; finance_minister_party = "CPC"; agriculture_minister_party = "RPC"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann"; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener";  coalition_dissent = 0; pro_republic -= 5; old_middle_ccf -= 3; new_middle_ccf -= 2; workers_ccf -= 3 if pro_republic < 50; workers_ccf -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50
go-to: let_it_happen_georgev if monarch != "Woodsworth"; let_it_happen_woodsworth if monarch == "Woodsworth"

@let_it_happen_georgev
on-arrival: prime_minister = "Brüning"; prime_minister_party = "RPC"; welfare -= 2;

The vote succeeds, and our government has resigned. Prime Minister Brüning has been appointed as the head of an acting government, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@let_it_happen_woodsworth
on-arrival: ccf_caretaker = 1

The vote succeeds, and our government has resigned. Prime Minister [+ prime_minister +] remains as the head of a caretaker government, but no longer has the power to enact new policies.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
